# Small Cmake script to test for the support of the new c++11 standard
# and the existence of the new standard template library libc++

# The script is working with cmake 2.6.4 and 2.8.10,
# so it should work also with all versions in between
CMake_Minimum_Required(VERSION 2.6 FATAL_ERROR)

Project(TestEnvironment C CXX)

Enable_Language(Fortran OPTIONAL)

Set(FairSoft_Version "DEV")

Set(Build_batch $ENV{BUILD_BATCH})
Set(fc_needed $ENV{Fortran_Needed})

# Check if all needed compileres are installed
If( fc_needed )
  If(CMAKE_CXX_COMPILER AND CMAKE_C_COMPILER AND CMAKE_Fortran_COMPILER)
    If (CMAKE_COMPILER_IS_GNUCC)
      Execute_process(COMMAND ${CMAKE_C_COMPILER} -dumpversion
                      OUTPUT_VARIABLE GCC_VERSION)
      If (GCC_VERSION VERSION_GREATER 4.8 OR GCC_VERSION VERSION_EQUAL 4.8)
        Message(STATUS "Found GCC Version >= 4.8")
        Message(STATUS "Found all necessary compilers.")
        Set(ALL_COMPILER_FOUND TRUE)
      Else()
        Message(WARNING "Found GCC Version < 4.8")
        Message(WARNING "At least GCC 4.8 is needed.")
      EndIf()
    ElseIf("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Clang")
      Execute_process(COMMAND ${CMAKE_C_COMPILER} --version
                      COMMAND grep version
                      OUTPUT_VARIABLE CLANG_VERSION_INFO
      )
      If(CLANG_VERSION_INFO MATCHES "^.*based on LLVM.*")
        String(REGEX REPLACE "^.*based on LLVM[ ]([0-9]+)\\.[0-9]+.*" "\\1" CLANG_MAJOR "${CLANG_VERSION_INFO}")
        String(REGEX REPLACE "^.*based on LLVM[ ][0-9]+\\.([0-9]+).*" "\\1" CLANG_MINOR "${CLANG_VERSION_INFO}")
      Else()
        String(REGEX REPLACE "^.*[ ]version[ ]([0-9]+)\\.[0-9]+.*" "\\1" CLANG_MAJOR "${CLANG_VERSION_INFO}")
        String(REGEX REPLACE "^.*[ ]version[ ][0-9]+\\.([0-9]+).*" "\\1" CLANG_MINOR "${CLANG_VERSION_INFO}")
      EndIf()
      Set(CLANG_VERSION "${CLANG_MAJOR}.${CLANG_MINOR}")
      If (CLANG_VERSION VERSION_GREATER 3.4 OR CLANG_VERSION VERSION_EQUAL 3.4)
        Message(STATUS "Found Clang Version >= 3.4")
        Message(STATUS "Found all necessary compilers.")
        Set(ALL_COMPILER_FOUND TRUE)
      Else()
        Message(WARNING "Found Clang Version < 3.4")
        Message(WARNING "At least Clang 3.4 is needed.")
      EndIf()
    ElseIf(${CMAKE_CXX_COMPILER_ID} STREQUAL "Intel")
      Message(STATUS "Found ${CMAKE_CXX_COMPILER} version ${CMAKE_CXX_COMPILER_VERSION}")
      if (${CMAKE_CXX_COMPILER_VERSION} VERSION_GREATER 15.0 OR ${CMAKE_CXX_COMPILER_VERSION} VERSION_EQUAL 15.0)
        Message(STATUS "Found all necessary compilers.")
        Set(ALL_COMPILER_FOUND TRUE)
      Else()
        Message(WARNING "At lease icc 15.0 is needed")
      EndIf()
    EndIf()
    Get_Filename_Component(_FC ${CMAKE_Fortran_COMPILER} NAME)
  EndIf()
Else()
  If(CMAKE_CXX_COMPILER AND CMAKE_C_COMPILER)
    Message(STATUS "Found all necessary compilers.")
    Message(STATUS "No Fortran compiler needed when building without simulation engines.")
    Set(ALL_COMPILER_FOUND TRUE)
  EndIf()
EndIf()

#Check for X11 which is mandatory for building ROOT

find_package(X11)

If (X11_FOUND AND X11_Xpm_FOUND AND X11_Xft_FOUND AND X11_Xext_FOUND)
  Message(STATUS "Found all necessary X11 features.")
  Set(COMPLETE_X11_FOUND TRUE)
Else()
  Set(COMPLETE_X11_FOUND FALSE)
EndIf()

if (NOT Build_batch)

  #Check for OpenGL and GLU installation
  find_package(OpenGL)

  If(OPENGL_FOUND AND OPENGL_GLU_FOUND)
    Message(STATUS "Found all necessary OpenGL features.")
    Set(COMPLETE_OPENGL_FOUND TRUE)
  Else()
    Set(COMPLETE_OPENGL_FOUND FALSE)
  EndIf()

EndIf()

#---Check for needed programs

Find_Program(WGET_FOUND NAMES wget)
Find_Program(CURL_EXE_FOUND NAMES curl)
If(WGET_FOUND OR CURL_EXE_FOUND)
  Set(DOWNLOADER_FOUND TRUE)
  If(CURL_EXE_FOUND)
    Set(HAS_CURL TRUE)
  EndIf()
EndIf()

# Need the library and headers for cmake
Find_Package(CURL)

Find_Program(MAKE_FOUND NAMES make)

Find_Program(BZIP2_FOUND NAMES bzip2)
Find_Program(GZIP_FOUND NAMES gzip)
Find_Program(UNZIP_FOUND NAMES unzip)
Find_Program(TAR_FOUND NAMES tar)

If(TAR_FOUND AND UNZIP_FOUND AND GZIP_FOUND AND BZIP2_FOUND)
  Set(UNPACKER_FOUND TRUE)
Endif()

Find_Program(SVN_FOUND NAMES svn)
Find_Program(GIT_FOUND NAMES git)

Find_Program(PATCH_FOUND NAMES patch)

Find_Package(OpenSSL)
# For Mac OSX 10.11 Apple remove the ssl header files
# If OpenSSL is not found search for ssl in brew installation path
If(NOT OPENSSL_FOUND)
 Set(OPENSSL_ROOT_DIR /usr/local)
 Find_package(OpenSSL)
EndIf()

# If we build with clang on linux also icu and mesa has to be build
# Mesa needs some additional programs, so we have to theck for them
Set(SPECIAL_CLANG_LINUX_FOUND TRUE)
If(CMAKE_CXX_COMPILER MATCHES "clang\\+\\+" AND CMAKE_SYSTEM_NAME MATCHES "Linux")
  Find_Program(MAKEDEP_FOUND NAMES makedepend)
  Find_Program(FLEX_FOUND NAMES flex)
  Find_Program(BISON_FOUND NAMES bison)
  If(NOT MAKEDEP_FOUND OR NOT FLEX_FOUND OR NOT BISON_FOUND)
    Set(SPECIAL_CLANG_LINUX_FOUND FALSE)
  Endif()
Endif()

Find_Program(AUTOCONF_FOUND NAMES autoconf)
Find_Program(AUTOMAKE_FOUND NAMES automake)
Find_Program(LIBTOOLIZE_FOUND NAMES libtoolize glibtoolize)
Find_Program(ACLOCAL_FOUND NAMES aclocal)

If(AUTOCONF_FOUND AND AUTOMAKE_FOUND AND LIBTOOLIZE_FOUND AND ACLOCAL_FOUND)
  Set(AUTOTOOLS_FOUND TRUE)
Endif()


If(DOWNLOADER_FOUND AND CURL_FOUND AND SVN_FOUND AND GIT_FOUND AND PATCH_FOUND AND MAKE_FOUND
   AND UNPACKER_FOUND AND COMPLETE_X11_FOUND AND COMPLETE_OPENGL_FOUND AND AUTOTOOLS_FOUND
   AND ALL_COMPILER_FOUND AND OPENSSL_FOUND AND SPECIAL_CLANG_LINUX_FOUND)
  Message(STATUS "Found all necessary programs in PATH.")
Else()
  If(NOT CMAKE_CXX_COMPILER)
    Message(STATUS "Could not find a C++ compiler. Was checking for ENV{CXX}. Either choose a differnt one or install it.")
  EndIf()
  If(NOT CMAKE_C_COMPILER)
    Message(STATUS "Could not find a C compiler. Was checking for ENV{CXX}. Either choose a differnt one or install it.")
  EndIf()
  If(NOT CMAKE_Fortran_COMPILER)
    Message(STATUS "Could not find any Fortran compiler. Please set the correct PATH or install one.")
  EndIf()
  If(NOT X11_FOUND)
    Message(STATUS "X11_INCLUDE_DIR: ${X11_INCLUDE_DIR}")
    Message(STATUS "X11_LIBRARIES: ${X11_LIBRARIES}")
    Message(STATUS "libX11 and X11 headers must be installed.")
  EndIf()
  If(NOT X11_Xpm_FOUND)
    Message(STATUS "X11_Xpm_INCLUDE_PATH: ${X11_Xpm_INCLUDE_PATH}")
    Message(STATUS "X11_Xpm_LIB: ${X11_Xpm_LIB}")
    Message(STATUS "libXpm and Xpm headers must be installed.")
  EndIf()
  If(NOT X11_Xft_FOUND)
    Message(STATUS "X11_Xft_INCLUDE_PATH: ${X11_Xft_INCLUDE_PATH}")
    Message(STATUS "X11_Xft_LIB: ${X11_Xft_LIB}")
    Message(STATUS "libXft and Xft headers must be installed.")
  EndIf()
  If(NOT X11_Xext_FOUND)
    Message(STATUS "X11_Xext_INCLUDE_PATH: ${X11_Xext_INCLUDE_PATH}")
    Message(STATUS "X11_Xext_LIB: ${X11_Xext_LIB}")
    Message(STATUS "libXext and Xext headers must be installed.")
  EndIf()
  If(NOT OPENSSL_FOUND)
    Message(STATUS "OPENSSL_INCLUDE_DIR: ${OPENSSL_INCLUDE_DIR}")
    Message(STATUS "OPENSSL_LIBRARIES: ${OPENSSL_LIBRARIES}")
    Message(STATUS "libssl and ssl headers must be installed.")
  EndIf()

  if (NOT Build_batch)
    If(NOT OPENGL_FOUND)
      Message(STATUS "OpenGL must be installed ")
    EndIf()
    If(NOT OPENGL_GLU_FOUND)
      Message(STATUS "Mesa GLU must be installed ")
    EndIf()
  EndIf()

  If(NOT CURL_FOUND)
    Message(STATUS "libcurl and curl headers must be installed")
  EndIF()
  If(NOT DOWNLOADER_FOUND)
    Message(STATUS "Neither wget nor curl found in PATH. You need to install one of them")
  EndIf()
  If(NOT MAKE_FOUND)
    Message(STATUS "make is not found in PATH. You need to install it")
  EndIf()
  If(NOT BZIP2_FOUND)
    Message(STATUS "bzip2 is not found in PATH. You need to install it")
  EndIf()
  If(NOT GZIP_FOUND)
    Message(STATUS "gzip is not found in PATH. You need to install it")
  EndIf()
  If(NOT UNZIP_FOUND)
    Message(STATUS "unzip is not found in PATH. You need to install it")
  EndIf()
  If(NOT TAR_FOUND)
    Message(STATUS "tar is not found in PATH. You need to install it")
  EndIf()
  If(NOT SVN_FOUND)
    Message(STATUS "svn is not found in PATH. You need to install it")
  EndIf()
  If(NOT GIT_FOUND)
    Message(STATUS "git is not found in PATH. You need to install it")
  EndIf()
  If(NOT PATCH_FOUND)
    Message(STATUS "patch is not found in PATH. You need to install it")
  EndIf()
 If(NOT AUTOMAKE_FOUND)
    Message(STATUS "automake is not found in PATH. You need to install it")
  EndIf()
  If(NOT AUTOCONF_FOUND)
    Message(STATUS "autoconf is not found in PATH. You need to install it")
  EndIf()
  If(NOT LIBTOOLIZE_FOUND)
    Message(STATUS "libtoolize is not found in PATH. You need to install it")
  EndIf()
  If(NOT ACLOCAL_FOUND)
    Message(STATUS "aclocal is not found in PATH. You need to install it")
  EndIf()
  If( SPECIAL_LINUX_CLANG_FOUND )
    If(NOT MAKEDEP_FOUND)
      Message(STATUS "makedep is not found in PATH. You need to install it")
    EndIf()
    If(NOT FLEX_FOUND)
      Message(STATUS "flex is not found in PATH. You need to install it")
    EndIf()
    If(NOT BISON_FOUND)
      Message(STATUS "bison is not found in PATH. You need to install it")
    EndIf()
  EndIf()
  Message(FATAL_ERROR "Please install the needed software. You can find a list with the package names for different Linux flavors in the file DEPENDENCIES.")
EndIf()

Include(CheckCXXCompilerFlag)

#---Check for cxx11
CHECK_CXX_COMPILER_FLAG("-std=c++11" HAS_CXX11)
If(HAS_CXX11)
  Message(STATUS "Current compiler does suppport -std=c++11 option.")
  Set(FAIRSOFT_CXX_FLAGS "${FAIRSOFT_CXX_FLAGS} -std=c++11")
Else()
  Message(STATUS "Current compiler does not suppport -std=c++11 option.")
EndIf()

#---Check for libc++
CHECK_CXX_COMPILER_FLAG("-std=c++11 -stdlib=libc++" HAS_LIBCXX)
If(HAS_LIBCXX)
  Message(STATUS "Current compiler does suppport -stdlib=libc++")
  Set(FAIRSOFT_CXX_FLAGS "${FAIRSOFT_CXX_FLAGS} -stdlib=libc++")
  String(STRIP ${FAIRSOFT_CXX_FLAGS} FAIRSOFT_CXX_FLAGS)
Else()
  Message(STATUS "Current compiler does not suppport -stdlib=libc++")
EndIf()

If(CMAKE_SYSTEM_NAME MATCHES Darwin)
  Exec_Program("sw_vers -productVersion | cut -d . -f1-2" OUTPUT_VARIABLE MAC_OS_VERSION)
  If(MAC_OS_VERSION MATCHES 10.8)
    Set(IsMacOsx108 TRUE)
  EndIf()
EndIf()

Set(Build_Python $ENV{BUILD_PYTHON})

If(Build_Python)
  Find_Package(PythonInterp)
  Find_Package(PythonLibs)
  If(NOT PYTHONLIBS_FOUND OR  NOT PYTHONINTERP_FOUND)
    Message(STATUS "PythonInterpreter: ${PYTHON_EXECUTABLE}")
    Message(STATUS "PythonLibraries: ${PYTHON_LIBRARIES}")
    Message(STATUS "PythonIncludes: ${PYTHON_INCLUDE_DIRS}")
    Message(FATAL_ERROR "You request to build the python bindings but you one of the above dependencies couldn't be found")
  EndIf()
EndIf()

Set(ROOT_VERSION $ENV{Root_Version})

Configure_File($ENV{SIMPATH}/test/configure.in $ENV{SIMPATH}/test/configure @ONLY)
Configure_File($ENV{SIMPATH}/scripts/fairsoft-config.in $ENV{SIMPATH_INSTALL}/bin/fairsoft-config @ONLY)

#---Create the file with version info
set(CMAKE_MODULE_PATH "$ENV{SIMPATH}/test")
include(GenerateVersionInfo)
